plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'fabric-loom' version '1.2-SNAPSHOT' apply false

	// https://github.com/Juuxel/LoomQuiltflower
	id 'io.github.juuxel.loom-quiltflower' version '1.10.0' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '20c7ec554a'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.3.0' apply false
}

preprocess {
	def mc191 = createNode('1.19.1'       , 1_19_01, 'yarn')
	def mc192 = createNode('1.19.2'       , 1_19_02, 'yarn')
	def mc193 = createNode('1.19.3'       , 1_19_03, 'yarn')
	def mc194 = createNode('1.19.4'       , 1_19_04, 'yarn')
	def mc200 = createNode('1.20'         , 1_20_00, 'yarn')
	def mc201 = createNode('1.20.1'       , 1_20_01, 'yarn')

	mc192.link(mc191, null)
	mc193.link(mc192, null)
	mc194.link(mc193, null)
	mc200.link(mc194, null)
	mc201.link(mc200, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
